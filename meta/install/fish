#!/usr/bin/env fish

touch $HOME/.hushlogin

set fishLocation (command -s fish)
if not grep -q "^$fishLocation\$" /etc/shells
  echo '-> Adding to available shells'
  echo $fishLocation | sudo tee -a /etc/shells
end

if not [ (finger $USER | awk '/Shell:/ { print $4 }') = $fishLocation ]
  echo '-> Making default shell'
  chsh -s $fishLocation
end

if not functions --query fisher
  echo '-> Install Fisher'
  curl -sL https://git.io/fisher | source && fisher install jorgebucaran/fisher
end

echo '-> Install Fisher plugins'
fisher update

echo '-> Universal variables'
set --universal FZF_LEGACY_KEYBINDINGS 0

# TODO Make play more nicely with SSH
echo '-> Italics'
tic $XDG_CONFIG_HOME/meta/italic-fonts/xterm-256color-italic.terminfo
set --query TERM; and set --erase TERM
set --universal --export TERM xterm-256color-italic
