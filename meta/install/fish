#!/usr/bin/env fish

touch $HOME/.hushlogin

set fishLocation (command -s fish)
if not grep -q "^$fishLocation\$" /etc/shells
  echo '-> Adding to available shells'
  echo $fishLocation | sudo tee -a /etc/shells
end

if not [ (finger $USER | awk '/Shell:/ { print $4 }') = $fishLocation ]
  echo '-> Making default shell'
  chsh -s $fishLocation
end

if not [ -d $dataDir/omf ]
  echo '-> Oh My Fish (you will need to hit Ctrl+D once installed)'
  source $configDir/omf/functions/bobthefish_colors.fish
  curl -L https://get.oh-my.fish > /tmp/omf.install.fish
  fish /tmp/omf.install.fish --config=$configDir/omf --path=$dataDir/omf
  rm /tmp/omf.install.fish
end

set fisherFile $configDir/omf/functions/fisher.fish
if not [ -f $fisherFile ]
  echo '-> Install Fisherman'
  curl --create-dirs -L https://git.io/fisher > $fisherFile
end

echo '-> Install Fisherman plugins'
source $fisherFile
fisher

echo '-> Universal variables'
set --universal FZF_LEGACY_KEYBINDINGS 0
set --universal theme_display_ruby no
set --universal theme_display_date no
set --universal theme_display_cmd_duration no
set --universal theme_nerd_fonts yes

# TODO Make play more nicely with SSH
echo '-> Italics'
tic $configDir/meta/italic-fonts/xterm-256color-italic.terminfo
set --query TERM; and set --erase TERM
set --universal --export TERM xterm-256color-italic
